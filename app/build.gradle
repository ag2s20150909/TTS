plugins {
    id 'com.android.application'
}

android {
    compileSdk 31
    buildToolsVersion '32.0.0'
    //签名设置
    signingConfigs {
        release {
            keyAlias 'key'
            keyPassword '1234567890'
            storeFile file('../public.jks')
            storePassword '1234567890'
            v1SigningEnabled true
            v2SigningEnabled true
            enableV3Signing = true
            enableV4Signing = true
        }
        debug {
            keyAlias 'key'
            keyPassword '1234567890'
            storeFile file('../public.jks')
            storePassword '1234567890'
        }
    }

    defaultConfig {
        applicationId "me.ag2s.tts"
        minSdkVersion 21
        targetSdk 31
        versionCode 1
        versionName "0.2_${releaseTime()}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            // 启用资源缩减
            shrinkResources true
            // Zipalign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            // 启用资源缩减
            shrinkResources false
            // Zipalign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        viewBinding true
    }



    compileOptions {
        //coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle false
    }

    android.applicationVariants.all {
        variant ->

            variant.outputs.all { output ->
                def buildType = variant.buildType.name
                println("buildType=" + buildType)
                if (buildType != "debug") {
                    //apk默认路径
                    String defaultOutputApkPath =
                            variant.getPackageApplicationProvider().get().outputDirectory.getAsFile().get().getPath() +
                                    File.separator
                    println("defaultOutputApkPath=" + defaultOutputApkPath)
                   //在项目根目录创建apk文件夹
                    def outputFileDir = new File("$rootDir" + File.separator, "release")
//                    println("outputFileDir=" + outputFileDir)
                    //设置自定义apk生成路径
                    variant.getPackageApplicationProvider().get().outputDirectory = outputFileDir
                }
            }


            def path = ((project.name != "app") ? project.name : rootProject.name.replace(" ", "")) + "_" +
                    variant.buildType.name + "_" +
                    "v"+variant.versionName + ".apk"
            variant.outputs.forEach {

                it.outputFileName = path
            }


    }




}

tasks.whenTaskAdded {task ->
    //新版的AGP7.1.0修改apk输出路径会导致文件找不到而出错（硬编码？？？），暂时禁用这个task
    if(task.name.contains("createReleaseApkListingFileRedirect")) {
        task.enabled = false
    }
}

dependencies {

    //coreLibraryDesugaring('com.android.tools:desugar_jdk_libs:1.1.5')

    implementation "androidx.recyclerview:recyclerview:1.2.1"

    //An HTTP & HTTP/2 client for Android and Java applications.
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.6"
    //noinspection GradleDependency
    //implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.5"


    // https://mvnrepository.com/artifact/com.squareup.okio/okio
    implementation 'com.squareup.okio:okio:3.0.0'




    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}

static def releaseTime() {

    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))

}